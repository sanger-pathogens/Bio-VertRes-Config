#!/usr/bin/env perl

package Bio::VertRes::Config::Main::BacteriaRnaSeqExpression;

# ABSTRACT: Create config scripts to map and run the rna seq expression pipeline
# PODNAME: bacteria_rna_seq_expression

=head1 SYNOPSIS

Create config scripts to map and run the rna seq expression pipeline

=cut

BEGIN { unshift( @INC, '../lib' ) }
BEGIN { unshift( @INC, './lib' ) }
BEGIN { unshift( @INC, '/software/pathogen/internal/prod/lib/' ) }
use Moose;
use Getopt::Long;
use Bio::VertRes::Config::CommandLine::LogParameters;
use Bio::VertRes::Config::CommandLine::ConstructLimits;
use Bio::VertRes::Config::Recipes::BacteriaRnaSeqExpressionUsingBwa;
use Bio::VertRes::Config::Recipes::BacteriaRnaSeqExpressionUsingSmalt;
use Bio::VertRes::Config::Recipes::BacteriaRnaSeqExpressionUsingTophat;
with 'Bio::VertRes::Config::CommandLine::ReferenceHandlingRole';

my $log_params = Bio::VertRes::Config::CommandLine::LogParameters->new( args => \@ARGV, script_name => $0 );

my ( $database, $config_base, $reference_lookup_file, $available_references, $reference, $type, $id, $species, $mapper,$protocol,
    $regeneration_log_file, $help );

GetOptions(
    'd|database=s'              => \$database,
    'c|config_base=s'           => \$config_base,
    'l|reference_lookup_file=s' => \$reference_lookup_file,
    'r|reference=s'             => \$reference,
    'a|available_references=s'  => \$available_references,
    't|type=s'                  => \$type,
    'i|id=s'                    => \$id,
    's|species=s'               => \$species,
    'm|mapper=s'                => \$mapper,
    'p|protocol=s'              => \$protocol,
    'b|regeneration_log_file=s' => \$regeneration_log_file,
    'h|help'                    => \$help,
);

$database              ||= 'pathogen_prok_track';
$config_base           ||= '/nfs/pathnfs01/conf';
$reference_lookup_file ||= '/lustre/scratch108/pathogen/pathpipe/refs/refs.index';
$protocol              ||= "StrandSpecificProtocol";
$regeneration_log_file ||= join('/',($config_base,'command_line.log'));
my $overwrite_existing_config_file = 0;

$log_params->log_file($regeneration_log_file);
$log_params->create();

( ( ( defined($available_references) && $available_references ne "" ) || ( $reference && $type && $id ) ) && !$help )
  or die <<USAGE;
Usage: bacteria_rna_seq_expression [options]
Run the RNA seq expression pipeline

# Search for an available reference
bacteria_rna_seq_expression -a "Stap"

# Run over a study
bacteria_rna_seq_expression -t study -i 1234 -r "Staphylococcus_aureus_subsp_aureus_EMRSA15_v1"

# Run over a single lane
bacteria_rna_seq_expression -t lane -i 1234_5#6 -r "Staphylococcus_aureus_subsp_aureus_EMRSA15_v1"

# Run over a file of lanes
bacteria_rna_seq_expression -t file -i file_of_lanes -r "Staphylococcus_aureus_subsp_aureus_EMRSA15_v1"

# Use the Standard FRT Protocol. The default is the Croucher Protocol
bacteria_rna_seq_expression -t study -i 1234 -r "Staphylococcus_aureus_subsp_aureus_EMRSA15_v1" -p "StandardProtocol"

# Run over a single species in a study
bacteria_rna_seq_expression -t study -i 1234 -r "Staphylococcus_aureus_subsp_aureus_EMRSA15_v1" -s "Staphylococcus aureus"

# Map with BWA instead of the SMALT (default). Tophat is also available.
bacteria_rna_seq_expression -t study -i 1234 -r "Staphylococcus_aureus_subsp_aureus_EMRSA15_v1" -m bwa

# This help message
bacteria_rna_seq_expression -h

USAGE

handle_reference_inputs_or_exit($reference_lookup_file, $available_references, $reference);


my $limits = Bio::VertRes::Config::CommandLine::ConstructLimits->new(
    input_type => $type,
    input_id   => $id,
    species    => $species
)->limits_hash;

my %parameters = (
    database                       => $database,
    config_base                    => $config_base,
    limits                         => $limits,
    reference_lookup_file          => $reference_lookup_file,
    reference                      => $reference,
    overwrite_existing_config_file => $overwrite_existing_config_file,
    protocol                       => $protocol
);

if ( defined($mapper) && $mapper eq 'bwa' ) {
    Bio::VertRes::Config::Recipes::BacteriaRnaSeqExpressionUsingBwa->new( \%parameters )->create();
}
elsif ( defined($mapper) && $mapper eq 'tophat' ) {
    Bio::VertRes::Config::Recipes::BacteriaRnaSeqExpressionUsingTophat->new( \%parameters )->create();
}
else {
    Bio::VertRes::Config::Recipes::BacteriaRnaSeqExpressionUsingSmalt->new( \%parameters )->create();
}

print "Once the data is available you can run these commands:\n\n";

print "Create symlinks to all your data\n";
print "  rnaseqfind -t $type -i $id -symlink\n\n";

print "Symlink to the spreadsheets of statistics per gene\n";
print "  rnaseqfind -t $type -i $id -symlink -f spreadsheet\n\n";

print "Symlink to the BAM files (corrected for the protocol)\n";
print "  rnaseqfind -t $type -i $id -symlink -f bam\n\n";

print "Symlink to the annotation for the intergenic regions\n";
print "  rnaseqfind -t $type -i $id -symlink -f intergenic\n\n";

print "Symlink to the coverage plots\n";
print "  rnaseqfind -t $type -i $id -symlink -f coverage\n\n";

print "More details\n";
print "  rnaseqfind -h\n";
